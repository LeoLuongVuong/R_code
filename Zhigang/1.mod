$PROBLEM    
;; 1. Based on: EPAR
;; 2. Description: 1 cmpt with addi+prop error
;; x1. Author: u0137111

$INPUT      ID STUDY WEEK TIME TAD EVID MDV AMT RATE CMT DV LNCONC LOQ OCC OCC_DOSETYPE ATI_titer ATI_CAT ATI_OVERALL NAB_CAT BWT BMI AGE SEX ALB CRP WBC HB PLT LYM COIMMU CO_STEROID_BASELINE CO_STEROID DIS_DUR DISEASE
$DATA       IFX_SC_PK_NONMEM_reduced_BLOQ.csv IGNORE=@  

$SUBROUTINE ADVAN13 TOL=9
$MODEL NCOMP=3
       COMP=(DEPOT)   ; Absorption compartment
       COMP=(CENTRAL) ; Central compartment
	   COMP=(AUC)     ; For calculating AUC 
	   
$PK
A_0(1)= 0

TVF1 = THETA(1)
F1   = TVF1* EXP(ETA(1))

;absorption
TVKA = THETA(2)
KA   = TVKA* EXP(ETA(2))

;Elimination
TVCL = THETA(3)
CL   = TVCL*EXP(ETA(3))

;distribution
TVV1 = THETA(4) 
V1   = TVV1*EXP(ETA(4))  

S1  = V1
KE  = CL/V1

$DES
DADT(1) = -KA*A(1)
DADT(2) =  KA*A(1) -KE*A(2)
DADT(3) =  A(2)/V1

$ERROR
AUC   = A(3)
IPRED = F
IRES  = DV - IPRED
W     = SQRT((THETA(5)*IPRED)**2 + THETA(6)**2)
IF (W.EQ.0) W = 1
IWRES = IRES/W 

LLOQ=0.1
IF (DV.GE.LLOQ) THEN
   F_FLAG = 0
   Y      = IPRED + EPS(1)*W
ELSE
   F_FLAG = 1
   Y      = PHI((LLOQ-IPRED)/W)
ENDIF

IF (ICALL.EQ.4) THEN
Y = IPRED +  EPS(1)*W
END IF

$THETA
(0, 0.576,1) ; 1 F
(0, 0.21648) ; 2 KA
(0, 0.3024)  ; 3 CL
(0, 2.89)    ; 4 V1
(0, 0.353)   ; 5 PROP ERROR
(0, 0.300)   ; 6 ADDI ERROR

$OMEGA  
 0.05        ; 1. omF1
 0.05        ; 2. omKA
 0.05        ; 3. omCL
 0.05        ; 5. omV1

$SIGMA 1 FIX

$EST METHOD = 1 INTER LAPLACIAN NUMERICAL MAXEVAL=9999 SADDLE_RESET=1 SIGL=9 NSIG=3
$COV Matrix=S PRINT=E

$TABLE ID STUDY TIME IPRED IWRES CWRES NOPRINT ONEHEADER FILE=sdtab1
$TABLE ID ATI_titer ATI_CAT ATI_OVERALL NAB_CAT BWT BMI AGE SEX ALB CRP WBC HB PLT LYM COIMMU CO_STEROID_BASELINE CO_STEROID DIS_DUR DISEASE NOPRINT NOAPPEND ONEHEADER FILE=cotab1
$TABLE ID ATI_CAT ATI_OVERALL NAB_CAT SEX COIMMU CO_STEROID_BASELINE CO_STEROID DIS_DUR DISEASE NOPRINT NOAPPEND ONEHEADER FILE=catab1
$TABLE ID F1 KA CL V1 NOPRINT NOAPPEND ONEHEADER FILE=patab1
$TABLE ID STUDY WEEK TIME TAD EVID MDV AMT RATE CMT DV LNCONC LOQ OCC OCC_DOSETYPE ATI_titer ATI_CAT ATI_OVERALL NAB_CAT BWT BMI AGE SEX ALB CRP WBC HB PLT LYM COIMMU CO_STEROID_BASELINE CO_STEROID DIS_DUR DISEASE F1 KA CL V1 PRED IPRED AUC IWRES CWRES NOPRINT NOAPPEND ONEHEADER FILE=fulltab1

